/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'hilmar' at '10/21/16 2:32 PM' with Gradle 3.1
 *
 * This generated file contains a commented-out sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.1/userguide/tutorial_java_projects.html
 */

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'

mainClassName = 'com.hugbunarfockers.gitcatgo.GitCatGo'

repositories {
    jcenter()
}

sourceSets {
    selenium
}

dependencies {
    // Application dependencies
	compile 'org.slf4j:slf4j-simple:1.7.21'
	compile 'com.sparkjava:spark-core:2.5'
	compile 'mysql:mysql-connector-java:6.0.5'
	compile 'org.xerial:sqlite-jdbc:3.14.2.1'

    // Unit test dependencies
    testCompile 'junit:junit:4.12'

    // Selenium dependencies
    seleniumCompile 'junit:junit:4.12'
    seleniumCompile 'org.seleniumhq.selenium:selenium-firefox-driver:3.0.1'
    seleniumCompile 'org.seleniumhq.selenium:selenium-chrome-driver:3.0.1'
    seleniumCompile 'com.codeborne:phantomjsdriver:1.3.0'
}

/*
 * Inspired by the answer of blootsvoets for the following StackOverflow question
 * http://stackoverflow.com/questions/4871656/using-gradle-to-build-a-jar-with-dependencies
 */

jar {
  manifest {
    attributes(
      'Main-Class': mainClassName
    )
  }
}

task fatJar(type: Jar) {
  manifest.from jar.manifest
  from {
    configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
  } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
  }
  with jar
}

task stage {
	dependsOn installDist
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

task selenium(type: Test, dependsOn: installDist) {
    testClassesDir = sourceSets.selenium.output.classesDir
    classpath = sourceSets.selenium.runtimeClasspath
}

check.dependsOn jacocoTestReport
